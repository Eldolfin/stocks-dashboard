# always enable playwright colored output
export FORCE_COLOR := "true"

help:
    just --list

_dc *args:
    #!/bin/sh
    if [ -n "$BACKEND_IMAGE" ] && [ -n "$FRONTEND_IMAGE" ]; then
        # Use CI configuration with pre-built images
        docker compose -f ../dev/docker-compose.ci.yml -f docker-compose.yml {{args}}
    else
        # Use standard development configuration
        docker compose -f ../dev/docker-compose.yml -f docker-compose.yml {{args}}
    fi

# Run playwright command in docker
docker-playwright +args:
    docker compose -f ../dev/docker-compose.yml down
    docker compose -f ../dev/docker-compose.yml up -d --build
    just _dc run tests-e2e npx -y playwright {{args}}

# Restart frontend & backend with a clean DB
docker:
    just _dc down tests-e2e
    just _dc up --exit-code-from tests-e2e --build tests-e2e

# run playwright tests outside of docker
run:
    npx playwright test --reporter=line

# Same as `test` but with the --ui argument passed to playwright for debugging
run-ui:
    npx playwright test --ui

# Generate expected screenshots
update-snapshots:
    just docker-playwright test --update-snapshots

# Ran by github actions
ci: docker
