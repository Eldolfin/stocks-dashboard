# always enable playwright colored output
export FORCE_COLOR := "true"

help:
    just --list

_dc *args:
    docker compose -f ../dev/docker-compose.yml -f docker-compose.yml {{args}}

# Run playwright command in docker
docker-playwright +args:
    just _dc run tests-e2e npx -y playwright {{args}}

# Restart frontend & backend with a clean DB
docker:
    just _dc down tests-e2e
    just _dc up --build tests-e2e

# run playwright tests outside of docker
run:
    npx playwright test --reporter=line

# Same as `test` but with the --ui argument passed to playwright for debugging
run-ui:
    npx playwright test --ui

# Generate expected screenshots
update-snapshots:
    just docker-playwright test --update-snapshots

# Ran by github actions
ci: docker
