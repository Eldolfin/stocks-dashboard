# always enable playwright colored output
export FORCE_COLOR := "true"

help:
    just --list

_dc *args:
    docker compose -f ../dev/docker-compose.yml -f docker-compose.yml {{args}}

# Run playwright command in docker
docker-playwright +args:
    docker compose -f ../dev/docker-compose.yml down
    docker compose -f ../dev/docker-compose.yml up -d --build
    just _dc run tests-e2e deno run -A npm:playwright {{args}}

# Restart frontend & backend with a clean DB
docker:
    just _dc down tests-e2e
    just _dc up --exit-code-from tests-e2e --build tests-e2e

# run playwright tests outside of docker
run:
    deno task test

# Same as `test` but with the --ui argument passed to playwright for debugging
run-ui:
    deno task test-ui

# Generate expected screenshots
update-snapshots:
    just docker-playwright test --update-snapshots

# Ran by github actions
ci: docker
